import win.ui;
/*DSG{{*/
var winform = win.form(text="ExceltoWeb";right=759;bottom=750;parent=...)
winform.add(
listview={cls="listview";left=19;top=84;right=739;bottom=723;edge=1;fullRow=1;z=1}
)
/*}}*/

//listview原地编辑，listview必须加上fullRow=1属性
winform.listview.onnotify = function(id,code,ptr){
    
    if(code=0xFFFFFFFE/*_NM_CLICK*/ ){
        var notifyMessage = winform.listview.getNotifyMessage(code,ptr);
        if( ! notifyMessage.iItem && notifyMessage.iSubItem ) return ; 
        
        var edit = winform.listview.editlable
        if( ! edit ){
            winform.listview.addCtrl(
                editlable = { 
                    cls="edit";font = LOGFONT( h = 11 );left = 0;top = 0;
                    right = 50;bottom = 50;autoResize = false ;hide = 1;edge = 1;  
                    wndproc = function( hwnd, message, wParam, lParam ){
                    	var update;
                        if( ( message = 0x8/*_WM_KILLFOCUS*/)  || message == 0x101/*_WM_KEYUP*/ && wParam == 0xD/*_VK_RETURN*/  ) {
                            update = true;
                        }
                        elseif(message == 0x201/*_WM_LBUTTONDOWN*/ || message == 0x202/*_WM_LBUTTONUP*/ ){
                        	var x,y = win.getMessagePos(lParam)
                        	var rc = edit.clientRect;  
                        	if( ! ::PtInRect(rc,x,y) ){
                        		update = true;
                        		edit.capture = false;
                        	}
                        	else{
                        		edit.capture = true;
                        	}
                        } 
                        
                        if( update ){
                            owner.parent.setItemText( owner.text, owner.listViewNotifyMessage.iItem
                                , owner.listViewNotifyMessage.iSubItem );
                            owner.show(false);
                        }
                    }  
                }
            )  
            edit = winform.listview.editlable;
        }
        edit.listViewNotifyMessage = notifyMessage; 
        edit.text=winform.listview.getItemText(notifyMessage.iItem,notifyMessage.iSubItem);
        var rc=winform.listview.getItemRect( notifyMessage.iItem,notifyMessage.iSubItem,,2/*_LVIR_LABEL*/ )
        rc.right += 5; rc.bottom += 5;
        edit.setRect(rc);
        edit.show(); 
        edit.setFocus();
        edit.capture = true;
    } 
}



winform.enableDpiScaling();
winform.show();

win.loopMessage();
return winform;
